plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}
// custom properties
def applicationIdArgument = project.findProperty('applicationId')
def applicationVersionCodeArgument = project.findProperty('versionCode')?.isInteger() ? project.findProperty('versionCode')?.toInteger() : null
def useDebugSigningForReleaseBuild = project.findProperty('useDebugSigningForReleaseBuild')?.toBoolean() ?: false

android {
    namespace 'org.fnives.android.qrcodetransfer'
    compileSdk 34

    defaultConfig {
        applicationId applicationIdArgument ?: "org.fnives.android.qrcodetransfer"
        minSdk 26
        targetSdk 34
        versionCode applicationVersionCodeArgument ?: 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file(System.getenv("BROQROLI_KEYSTORE") ?: 'debug.keystore')
            keyAlias System.getenv("BROQROLI_KEY") ?: ''
            keyPassword System.getenv("BROQROLI_KEY_PASS") ?: ''
            storePassword System.getenv("BROQROLI_PASS") ?: ''
        }
    }

    buildTypes {
        release {
            if (useDebugSigningForReleaseBuild) {
                signingConfig signingConfigs.debug
            } else {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.4'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    def compose_ui_version = '1.5.4'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.activity:activity-compose:1.8.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.5.4'

    // qr code
    implementation "com.google.zxing:core:3.5.2"

    // permission
    implementation 'com.google.accompanist:accompanist-permissions:0.32.0'

    // camerax
    def camerax_version = "1.3.0"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"
    implementation "androidx.camera:camera-extensions:$camerax_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}